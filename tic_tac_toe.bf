lista (minus;plus;|;X;O; ;enter;nic;nic;1;2;3;4;5;6;7;8;9;gameloop;nic;warunki)
>>+++++[<+++++++++<+++++++++>>-]<-->
>++++++++[<++++++++++++++++>-]<---->
>>++++++++[<++++++++++<+++++++++++>>-]<->
>++++[<++++++++>-]
++++++++++>
>>

+>+>+>+>+>+>+>+>+
>+++++++++

[<]++++++[<++++++++>-]<+

.<<<<<.>>>>>+.<<<<<.>>>>>+.<.
<<<<<<.>.<.>.<.>>>>>>.
>+.<<<<<.>>>>>+.<<<<<.>>>>>+.<.
<<<<<<.>.<.>.<.>>>>>>.
>+.<<<<<.>>>>>+.<<<<<.>>>>>+.<.
>[-]


>>[>]<
[


check if round is even or uneven
[->>>>>>+>+>+[<]<<<<<]
>>>>>>>>[-<<<<<<<<+>>>>>>>>]
-<+>
[++
<--[>-<<<]>[<]<[<<]>
>--[>>-<<<<]>[<<]<[<]>
>>>
]

<[-]+<<<<
,>++++++[<-------->-]

add to specified cell
+<<+++++++++
[
<<[<]>
[-<+>]
>[>]>>-[>-]
>[-
<<<<[<]<
-[>>[>]>>>>>>-<[-]<<<<<<+[<]]
>[<]<+>>[>]>>>>>>
[-<<<<<<<[<]<+>>[>]>>>>>>]
<[[-]<<<<<<[<]<+>>[>]>>>>>]
[-]<<]
<<<[<]>>>+<<
-]
>[-]>[-]
move all cells back
<<+++++++++
[
<<[<]<[
->+<
]>>[>]>
-]

print map
+++>++<
[-
>>+++

[-
print space or O or X
>+>+<<
<<<<[<]
+>-[
<+>-[
<+>-
<[<]<<<<.[>]>[>]>[>]>>>>->-<<<<<<[<]
]>[>]>>>>>[-<-
<<<<<[<]<[<]<<<.[>]>[>]>[>]>>>>>
]<<<<<<[<]
]>[>]>>>>[->-
<<<<<[<]<[<]<[<]<<.[>]>[>]>[>]>>>>
]
print | or enter
<[>+>+<<-]>>[-<<+>>]
+<[>-
<<<<<<[<]<[<]<<<<<.[>]>[>]>[>]>>>>
[-]]>[-
<<<<<<[<]<[<]<.>>[>]>[>]>>>>>
]
<<
]
print horizontal divider
<[-
<<<[<]<[<]<<<<<<<
.>.<.>.<.[>]<.>>[>]>[>]
][<]>
]
move all cells back
<<+++++++++
[
<<[<]<[
->+<
]>>[>]>
-]
check who wins
horizontal lines
copy cells (first 3)
<<[<]>-[[>]>+>+<<<[<]>-]<+>>
>[>]>>[<<<[<]<+>>[>]>>-]
<<<[<]>-[[>]>>+>+<<<<[<]>-]<+>>
>[>]>>>[<<<<[<]<+>>[>]>>>-]
<<<<[<]>-[[>]>>>+>+<<<<<[<]>-]<+>>
>[>]>>>>[<<<<<[<]<+>>[>]>>>>-]
compare cells
<<[->>+>+>+<<<<]>>>>[-<<<<+>>>>]+ copy middle cell
<<<<<[->-<]>[[-]>>>>-<<<<]
>>>>[
<[-<<<+>>>]+
<<[-<->]<[[-]>>>-<<<]
>+>[<-]
<[<]>[>>[-]<<-]
>>[>[>+<[-]]<[-]]>>[-<<<<<<+>>>>>>]
<[-]]<<<<<
print who wins
[
<<[-]+>>
++++++++++[>++++++++<-]>-.
>++++++[<++++++>-]<--.
>++[<--------->-]<.
+++++++++++.
+++++++.
--.
>++[<----------->-]<.
<<<[<]<[<]<<.>>>[>]>[>]>>
>>>+<-[->-
<<<<<<[<]<[<]<<<<.[>]>[>]>[>]>>>>>
]>[-
<<<<<<[<]<[<]<<<.[>]>[>]>[>]>>>>>
]<<<
]>>[-]>[-]>[-]<<<<


,[-]<<-]
